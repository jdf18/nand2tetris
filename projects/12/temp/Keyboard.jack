// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int KEYBOARD, MAXLENG;

    /** Initializes the keyboard. */
    function void init() {
        let KEYBOARD = 24576;
        let MAXLENG = 64;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(KEYBOARD);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        while (c = 0) {
            let c = Memory.peek(KEYBOARD);
        }
        if (~(c = 128) & ~(c = 129)) {
            do Output.printChar(c);
        }
        return c;
    }

    function String readLine(String message) {
        var char c;
        var String str;

        let str = String.new(MAXLENG);
        do Output.printString(message);

        let c = Keyboard.readChar();
        while (~(c = 128) & (str.length() < MAXLENG)) {
            if (c = 129) {
                do Output.backSpace();
                do str.eraseLastChar();
            } else {
                do str.appendChar(c);
            }
            while (~(c = 0)) {
                let c = Memory.peek(KEYBOARD);
            }
            let c = Keyboard.readChar();
        }
        while (~(c = 0)) {
            let c = Memory.peek(KEYBOARD);
        }
        do Output.println();

        return str;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var char c;
        var String str;

        let str = String.new(MAXLENG);
        do Output.printString(message);

        let c = Keyboard.readChar();
        while (~(c = 128) & (str.length() < MAXLENG)) {
            if (c = 129) {
                do Output.backSpace();
                do str.eraseLastChar();
            } else {
                do str.appendChar(c);
            }
            while (~(c = 0)) {
                let c = Memory.peek(KEYBOARD);
            }
            let c = Keyboard.readChar();
        }
        while (~(c = 0)) {
                let c = Memory.peek(KEYBOARD);
            }
        do Output.println();

        return str.intValue();
    }
}
